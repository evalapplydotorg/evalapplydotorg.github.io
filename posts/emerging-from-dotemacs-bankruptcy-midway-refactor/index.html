<!doctype html><html lang=en prefix="og: https://ogp.me/ns#"><title>Emerging from dotemacs bankruptcy the hard way: the midway refactor</title><meta charset=utf-8><meta name=viewport content="width=device-width,initial-scale=1"><meta name=author content="Aditya Athalye"><meta name=description content="Or the one in which we confront our elisp n00bishness and try to be better at using it. And we learn new habits to understand our Emacs better. Better late than never."><meta name=keywords content="systems thinking,functional programming,architecture,software design,technology leadership,devops,clojure"><meta property="og:title" content="Emerging from dotemacs bankruptcy the hard way: the midway refactor"><meta property="og:description" content="Or the one in which we confront our elisp n00bishness and try to be better at using it. And we learn new habits to understand our Emacs better. Better late than never."><meta property="og:locale" content="en_GB"><meta property="og:type" content="website"><meta property="og:url" content="https://www.evalapply.org/posts/emerging-from-dotemacs-bankruptcy-midway-refactor/index.html"><link rel="stylesheet preload" as=style href=https://www.evalapply.org/static/css/style.css><link href=https://www.evalapply.org/index.xml rel=alternate type=application/rss+xml title="Aditya Athalye writes and works here."><link rel=canonical href=https://www.evalapply.org/posts/emerging-from-dotemacs-bankruptcy-midway-refactor/index.html><link rel=icon type=image/vnd.microsoft.icon href=https://www.evalapply.org/static/img/favicon.ico><div id=the-very-top class="stack center box"><header id=site-header><div class="box invert stack"><div class="with-sidebar site-header"><a class="box icon" href=https://www.evalapply.org/index.html><img src=https://www.evalapply.org/static/img/Lisp_logo.svg loading=lazy alt="Aditya Athalye writes and works here."></a><div class=stack><h1 class="site-header title">Aditya Athalye writes and works here.</h1><nav class="cluster site-header site-header:nav-items"><a href=https://www.evalapply.org/index.html>&#955; hello</a>
<a href=https://www.evalapply.org/posts/index.html>&#9753; read</a>
<a href=mailto:hello@evalapply.org>&nbsp;&#9993; write</a>
<a href=https://www.evalapply.org/index.html#hire>$ hire</a>
<a href=https://www.evalapply.org/now.html#main>&#171; now &#187;</a>
<a href=https://www.evalapply.org/index.xml>&ap; feed</a></nav></div></div></div></header><main id=main><article id=blog-post class=stack><header><div class=stack><div class=title>Emerging from dotemacs bankruptcy the hard way: the midway refactor</div><div class="cluster post-meta"><span>&uarr; <a href=#site-header rel=bookmark>menu</a></span>
<span>&darr; <a href=#blog-post-footer rel=bookmark>discuss</a></span></div><div class=summary>Or the one in which we confront our elisp n00bishness and try to be better at using it. And we learn new habits to understand our Emacs better. Better late than never.</div><div class="cluster post-meta"><span class=author>By: Adi</span>
<span class=date>Published: 2023-07-23</span>
<span class=date>Updated: 2023-07-23</span>
<span class=tags>Tags: / <a href=https://www.evalapply.org/tags/programming/index.html#main>#programming</a> / <a href=https://www.evalapply.org/tags/emacs/index.html#main>#emacs</a> / <a href=https://www.evalapply.org/tags/howto/index.html#main>#howto</a> / <a href=https://www.evalapply.org/tags/recurse_center/index.html#main>#recurse_center</a></span></div><hr></div></header><section class=stack><div class="footnotes box invert"><p>Series: <a href=../emerging-from-dotemacs-bankruptcy/index.html#main>prelude</a> → <a href=../emerging-from-dotemacs-bankruptcy-init-begins/index.html#main>init begins</a> → <strong>(<a href=../emerging-from-dotemacs-bankruptcy-midway-refactor/index.html#main>midway refactor</a>)</strong> → <a href=../emerging-from-dotemacs-bankruptcy-packages/index.html#main>packages</a> → <a href=../emerging-from-dotemacs-bankruptcy-getting-about/index.html#main>getting about</a> → <a href=../emerging-from-dotemacs-bankruptcy-ide-experience/index.html#main>IDE (ft. Clojure)</a> → <a href=https://github.com/adityaathalye/dotemacs>.emacs.d</a></div><p>Part way through "the plan", I realised a few things:<ul><li>I had been learning elisp by osmosis. I needed to make some of it explicit.<li>A subtle bug or two had crept in.<li>Better code layout and organisation would ease my life.<li>I should have added more "better defaults" already.</ul><p>Areas for improvement presented themselves, such as purpose and usage of <code>setq</code>, <code>put</code>, <code>setq-default</code>. So I started using <code>M-x describe-*</code> a lot. I wish I had habituated to this when I was new to Emacs. If you are just starting off, wiring <code>describe-*</code> into muscle memory early on is a good idea. It will help you:<ul><li>demystify what your Emacs is made of and how it works under the hood<li>learn to use what you have, with expertise (keybindings, modes, features etc.)<li>gain confidence to improve, debug, and tailor Emacs to your needs<li>discover useful and creative ways to do more things with Emacs</ul><p><code>describe-*</code> s I have used frequently while developing the dotemacs:<pre class=elisp><code>describe-variable
describe-function
describe-command
describe-package
</code></pre><p>Other <code>describe-*</code> s I have used so far:<pre class=elisp><code>describe-mode
describe-key
describe-keymap
describe-bindings
describe-face
describe-font
</code></pre><p>And when I don't know what's what in the first place, <code>apropos</code> comes to the rescue. Learn that too!<p>I also enriched help with the <code>helpful</code> package, discovered in <a href=https://systemcrafters.net/emacs-from-scratch/helpful-ui-improvements/>episode 2 of <em>Emacs from Scratch</em></a> by systemcrafters.net.<pre class=elisp><code>(use-package helpful ; h/t systemcrafters.net
  ;; https://github.com/Wilfred/helpful
  :ensure t
  :bind ((&quot;C-h f&quot; . #&#39;helpful-callable)
         (&quot;C-h F&quot; . #&#39;helpful-function) ; exclude macros
         (&quot;C-h v&quot; . #&#39;helpful-variable)
         (&quot;C-h k&quot; . #&#39;helpful-key)
         (&quot;C-h x&quot; . #&#39;helpful-command)
         ;; Lookup the current symbol at point. C-c C-d is
         ;; a common keybinding for this in lisp modes.
         (&quot;C-c C-d&quot; . #&#39;helpful-at-point)))
</code></pre><p>Another thing I discovered was that I had created subtle bugs by not knowing Directory v/s File naming conventions. Directory names should always be correctly terminated, so they are interpreted and handled correctly. So I started using <code>file-name-as-directory</code> since.<pre class=elisp><code>(defvar dotemacs-savefile-dir
  (file-name-as-directory
   (expand-file-name &quot;savefile&quot; dotemacs-dir))
  &quot;This folder stores all the automatically generated save/history-files.&quot;)

(unless (file-exists-p dotemacs-savefile-dir)
  (make-directory dotemacs-savefile-dir))
</code></pre><p>Pre-debugging, the savefile dir was <code>(expand-file-name "savefile" dotemacs-dir)</code>, and that broke package configuration statements that tried to create savefiles.<p>On similar lines, I have noticed several dotemacsen in the wild use <code>concat</code> to construct paths. That seems more error-prone (bad path construction), and not portable, say, if the same dotemacs needs work on Linux and on Windows. Presumably the many inbuilt file and directory manipulation functions exist to handle the many platform specific oddities for us. I think it's wise to look these up for anything to do with file handling (<a href=https://www.gnu.org/software/emacs/manual/html_node/emacs/Files.html>see manual</a>). Some useful ones are:<pre class=elisp><code>file-exists-p
expand-file-name
file-name-as-directory
make-directory
make-empty-file
load-file
load-file-name
</code></pre><p>On another level, now that more code was being added, the conceptual layout of said code felt ripe for cleanup. I also had a better sense of how and why to do this by dint of having read other peoples layout choices. So, many globals were consolidated and some more added a la "better defaults". This is where neater use of `setq` and `setq-default` helped.<p>And as a <em>pièce de résistance</em>, I also chose to constrain my Emacs to <code>v28.1</code> or higher. Now that this stable version is widely available, I don't anticipate trouble in any of my personal or work environments.<p>The init.el so far (<a href=../emerging-from-dotemacs-bankruptcy-packages/index.html#main>previously</a>, <a href=../emerging-from-dotemacs-bankruptcy-init-begins/index.html#main>even earlier</a>)…<details class="box invert stack"><summary><code>After the mid-way refactor, and some more additions</code><hr></summary><pre class=elisp><code>;;; package --- init.el  -*- lexical-binding: t -*- --- My Emacs configuration.

;;; Commentary:

;;; This file is not part of GNU Emacs.

;;; Author: Aditya Athalye
;;; Created on: 30 June 2023
;;; Copyright (c) 2023 Aditya Athalye

;;; License:
;;; This program is free software; you can redistribute it and/or
;;; modify it under the terms of the MIT license, which is included
;;; with this distribution. See the LICENCE.txt file.

;;; Code:

;; Set the low bar Emacs compatibility high
(defvar dotemacs-min-version &quot;28.1&quot;)

(when (version&lt; emacs-version dotemacs-min-version)
  (error &quot;We demand spiffy new Emacs, at least v%s, but you have v%s&quot;
         dotemacs-min-version
         emacs-version))

;; ;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;; DIRECTORY STRUCTURE
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;; Take clues from bbatsov/prelude, except keep structure relative to our
;; initial dotemacs-dir path. This way we can start the user&#39;s emacs via
;; ~/.emacs.d symlinked to the dotemacs repo, and develop/debug against
;; the repo without potentially overwriting transient state files of the
;; daily driver .emacs.d.

(defvar dotemacs-dir
  (file-name-directory (or load-file-name (buffer-file-name)))
  &quot;The dotemacs&#39; root.  Normally it should be ~/.emacs.d.&quot;)

(defvar dotemacs-custom-file (expand-file-name &quot;custom.el&quot; dotemacs-dir)
  &quot;Make Emacs add customisations here, instead of the init file.
Usually customisations made from the UI go into `custom-file&#39;.&quot;)
(setq custom-file dotemacs-custom-file)
(unless (file-exists-p dotemacs-custom-file)
  (make-empty-file dotemacs-custom-file))
(load-file custom-file) ; load *now*, instead of unpredictable load sequence

(defvar dotemacs-savefile-dir (file-name-as-directory
                               (expand-file-name &quot;savefile&quot; dotemacs-dir))
  &quot;This folder stores all the automatically generated save/history-files.&quot;)
(unless (file-exists-p dotemacs-savefile-dir)
  (make-directory dotemacs-savefile-dir))

;; ;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;; BETTER DEFAULTS
;; ;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;; via
;; - technomancy/better-defaults
;; - bbatsov/prelude
;; - vedang/emacs-up
;; - suvratapte/dot-emacs-dot-d

;; SPACES OVER TABS,
;; but keep up conventional appearances of 8 character wide tabs.
;; NOTE: Use `C-q TAB` to insert literal tabs.
(setq-default indent-tabs-mode nil
              tab-width 8
              fill-column 80)

(setq
 ;; PERFORMANCE
 ;; gc ~100MB for better overall performance. ~50-100MB is recommended
 ;; these days over the long-obsolete default of ~8MB.
 gc-cons-threshold (* 100 1024 1024)
 ;; Large files freeze Emacs. Warn for files over ~100MB.
 large-file-warning-threshold (* 100 1024 1024)
 ;; Always load newest byte code. cf. bbatsov/prelude
 load-prefer-newer t

 ;; INTERACTIONS
 inhibit-startup-message t
 ring-bell-function &#39;ignore ; no beeps
 require-final-newline t ; always well-form files
 confirm-kill-emacs &#39;y-or-n-p ; instead of disabling &#39;C-x C-c&#39;
 create-lockfiles nil
 tab-always-indent &#39;complete
 tab-first-completion &#39;word
 uniquify-buffer-name-style &#39;forward
 uniquify-after-kill-buffer-p t ; rename after killing uniquified
 uniquify-ignore-buffers-re &quot;^\\*&quot;) ; spare special buffers

;; MORE INTERACTIONS
;; http://ergoemacs.org/emacs/emacs_save_restore_opened_files.html
(global-auto-revert-mode 1) ; auto-revert buffer if file-on-disk changes
(delete-selection-mode t) ; delete selection for any keypress
(add-hook &#39;before-save-hook &#39;delete-trailing-whitespace)
(fset &#39;yes-or-no-p &#39;y-or-n-p) ; typing &quot;yes/no&quot; gets annoying fast
(global-unset-key (kbd &quot;C-z&quot;)) ; avoid fat fingering `suspend-frame&#39;

;; UTF-8 as default encoding
;; ref: http://ergoemacs.org/emacs/emacs_encoding_decoding_faq.html
(set-language-environment &quot;UTF-8&quot;)

;; ;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;; VISUAL AESTHETICS
;; ;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;; Go easy on the eyes. This high-contrast darkmode theme is built
;; into Emacs, as of Emacs version 28.1
(load-theme &#39;modus-vivendi)

;; More screen real estate
(scroll-bar-mode 0)
(tool-bar-mode 0)
(tooltip-mode 0) ; disable popup, make help text appear in echo area
(menu-bar-mode 0)
(column-number-mode t)
(set-fringe-mode &#39;(5 . 13)) ;; describe variable fringe-mode
(global-display-line-numbers-mode 1) ; always show line numbers
(global-hl-line-mode +1)

;; Tweak Font sizes globally, and also set line number mode
(defun adi/set-frame-font--default ()
  &quot;Interactively set default frame font for day to day work.&quot;
  (interactive)
  (set-frame-font &quot;-PfEd-DejaVu Sans Mono-normal-normal-normal-*-13-*-*-*-m-0-iso10646-1&quot;)
  (global-display-line-numbers-mode -1))

(defun adi/set-frame-font--pair-prog ()
  &quot;Interactively set frame font for pair programming.&quot;
  (interactive)
  (set-frame-font &quot;-PfEd-DejaVu Sans Mono-normal-normal-normal-*-16-*-*-*-m-0-iso10646-1&quot;)
  (global-display-line-numbers-mode 1))

(defun adi/set-frame-font--code-demo ()
  &quot;Interactively set frame font for presentations and demos.&quot;
  (interactive)
  (set-frame-font &quot;-1ASC-Liberation Mono-normal-normal-normal-*-28-*-*-*-m-0-iso10646-1&quot;)
  (global-display-line-numbers-mode -1))

;; Ensure we always start Emacs with the default font.
(adi/set-frame-font--default)

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;; Package management
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

(require &#39;package)
;; Explicitly set the exact package archives list
(setq package-archives &#39;((&quot;melpa&quot; . &quot;https://melpa.org/packages/&quot;)
                         (&quot;org&quot; . &quot;https://orgmode.org/elpa/&quot;)
                         (&quot;elpa&quot; . &quot;https://elpa.gnu.org/packages/&quot;)))
;; Set package download directory relative to the dotemacs-dir
(setq package-user-dir (file-name-as-directory
                        (expand-file-name &quot;elpa&quot; dotemacs-dir)))

(package-initialize)
(unless package-archive-contents
  (package-refresh-contents))

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;; Use use-package
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

;; Ian Y.E. Pan&#39;s tutorial is a nice quick overview.
;; https://ianyepan.github.io/posts/setting-up-use-package/

(unless (package-installed-p &#39;use-package)
  (package-install &#39;use-package))

(eval-when-compile
  (add-to-list &#39;load-path package-user-dir)
  (require &#39;use-package))

(setq use-package-always-ensure t)
(setq use-package-expand-minimally t) ; set nil to debug use-package forms

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;; All the packages!
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

;; Packages useful to configure packages

;; radian-software/blackout to tune major/minor mode names
;; in modeline. It unifies functionality of the mutually
;; confusing modeline lighters delight, diminish, and dim.
;; It integrates seamlessly with use-package.
(use-package blackout)

(use-package helpful ; h/t systemcrafters.net
  ;; https://github.com/Wilfred/helpful
  :ensure t
  :bind ((&quot;C-h f&quot; . #&#39;helpful-callable)
         (&quot;C-h F&quot; . #&#39;helpful-function) ; exclude macros
         (&quot;C-h v&quot; . #&#39;helpful-variable)
         (&quot;C-h k&quot; . #&#39;helpful-key)
         (&quot;C-h x&quot; . #&#39;helpful-command)
         ;; Lookup the current symbol at point. C-c C-d is
         ;; a common keybinding for this in lisp modes.
         (&quot;C-c C-d&quot; . #&#39;helpful-at-point)))

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;; General text viewing and editing
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

;; Enable narrowings to enhance focus, and reduce accidental
;; edits of nonfocus areas (thanks to save-restrictions).
;; h/t bbatsov/prelude
;; Note: `C-x n w` makes all visible again.
(put &#39;narrow-to-region &#39;disabled nil)
(put &#39;narrow-to-page &#39;disabled nil)
(put &#39;narrow-to-defun &#39;disabled nil)

;; COMplete ANYthing, please!
;; TODO: instead try radian&#39;s completion system
;;; ref: https://github.com/radian-software/radian
(use-package company
  :bind (:map global-map
              (&quot;TAB&quot; . company-indent-or-complete-common)
              ;; Got to love the name hippie-expand. Use this for general
              ;; expansions, because company-complete is good for the
              ;; current/narrow-case expansions. By default, `M-/` binds
              ;; to the less powerful `dabbrev-expand`. To alter search
              ;; options, :config the hippie-expand-try-functions-list.
              (&quot;M-/&quot; . hippie-expand))
  :config (setq company-idle-delay 0.1
                company-minimum-prefix-length 2)
  (global-company-mode t)
  :blackout)

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;; Lispy editing support
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

;; Tweak settings of built-in paren package
(use-package paren
  :ensure nil                    ; it already exists, don&#39;t try to search online
  :init
  (setq show-paren-delay 0)
  :config
  (show-paren-mode t)
  :blackout)

(use-package paredit
  ;; Handy configs available at the wiki https://www.emacswiki.org/emacs/ParEdit
  ;; including combining with eldoc, &quot;electric&quot; enabled modes etc.
  :ensure t
  :bind
  ((&quot;M-[&quot; . paredit-wrap-square)
   (&quot;M-{&quot; . paredit-wrap-curly))
  :hook ((emacs-lisp-mode lisp-interaction-mode)
         . paredit-mode)
  :blackout)

(use-package magit
  :diminish)

(provide &#39;init)
  ;;; init.el ends here
</code></pre></details><p>Our current place in the rough plan:<ul><li>[✓] Set the very preliminaries.<li>[✓] Set up package management. I'll probably stick with the old familiars; elpa and melpa. I'm not sure about straight.el at this time.<li>[✓] Choose <code>use-package</code> to get and configure each package. I like how neat configs are, when defined with use-package.<li>[✓] Unexpectedly refactor the whole thing.<li>[WIP] Make completions and "getting about" work (the right mix of ivy, consul, swiper, company, helm, imenu). Someone mentioned newer alternatives to helm. Have a look at that.<li>[WIP] Fix general text editing stuff (keybindings, multiple cursors, snippets etc.)<li>[WIP] Add support for favourite programming languages.<ul><li>[✓] Emacs Lisp<li>many others…</ul><li>org-mode specifics<li>then let's see…</ul><p>Although not quite… This post is for posterity. By now, I have done a fair bit of work on the "getting about" bits of the plan, viz. navigation, movement, window layout, UI state, discoverability and so forth. That is for the next post.</section><footer id=blog-post-footer class="footer stack"><nav class=cluster><span>&uarr; <a href=#blog-post rel=bookmark>title</a></span>
<span>&uarr; <a href=#site-header rel=bookmark>menu</a></span>
<span><b>Y</b> <a href="https://news.ycombinator.com/submitlink?u=https%3A%2F%2Fwww.evalapply.org%2Fposts%2Femerging-from-dotemacs-bankruptcy-midway-refactor%2Findex.html&t=Emerging%20from%20dotemacs%20bankruptcy%20the%20hard%20way%3A%20the%20midway%20refactor">discuss at HN</a></span>
<span>&rarr; <a href=mailto:weblog@evalapply.org>email comments</a></span>
<span><a class=site-feed href=https://www.evalapply.org/index.xml>Blog feed</a></span></nav><hr><form class=cluster action=https://buttondown.email/api/emails/embed-subscribe/evalapply method=post target=popupwindow onsubmit="window.open('https://buttondown.email/evalapply','popupwindow')"><span>Occasional newsletter</span>
<input type=email name=email id=bd-email>
<span><input type=submit value="Get the eval/apply dispatch">
<em>(thanks, <a href=https://buttondown.email target=_blank>Buttondown</a>!)</em></span></form></footer></article></main><footer id=site-footer><hr><div class="box invert footer stack"><p>&copy; 2024,
<a href=http://adityaathalye.com target=_blank>Aditya Athalye</a>.
<span>All content licensed
<a rel=license href=http://creativecommons.org/licenses/by-sa/4.0/>CC BY-SA 4.0
</a>, except where noted otherwise.</span>
Built with
<a href=https://github.com/adityaathalye/shite>shite</a>.</div></footer><script defer src=https://static.cloudflareinsights.com/beacon.min.js data-cf-beacon='{"token": "2a55df7e78f941c29a35207cedd0f66c"}'></script></div>